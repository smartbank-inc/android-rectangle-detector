plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
    id 'org.jetbrains.dokka-javadoc'
}

group = 'com.github.smartbank-inc'

android {
    namespace 'jp.co.smartbank.rectangledetector'
    compileSdk compile_sdk_version

    defaultConfig {
        minSdkVersion min_sdk_version
        targetSdkVersion target_sdk_version
        versionCode version_code
        versionName version_name

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = '11'
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dokka {
    dokkaPublications.javadoc {
        outputDirectory.set(layout.buildDirectory.dir("dokka/javadoc"))
    }

    dokkaSourceSets.configureEach {
        documentedVisibilities.set([
            org.jetbrains.dokka.gradle.engine.parameters.VisibilityModifier.Public,
            org.jetbrains.dokka.gradle.engine.parameters.VisibilityModifier.Protected
        ])

        suppressInheritedMembers.set(false)

        jdkVersion.set(11)

        sourceLink {
            localDirectory.set(file("src/main/java"))
            remoteUrl("https://github.com/smartbank-inc/android-rectangle-detector/tree/main/library/src/main/java")
            remoteLineSuffix.set("#L")
        }
    }
}

dependencies {
    implementation 'org.opencv:opencv:4.12.0'
    testImplementation 'androidx.test:core:1.4.0'
    testImplementation 'androidx.test:runner:1.4.0'
    testImplementation 'androidx.test:rules:1.4.0'
    testImplementation 'androidx.test.ext:junit:1.1.3'
    testImplementation "junit:junit:$junit_version"
    testImplementation 'org.robolectric:robolectric:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = 'com.github.smartbank-inc'
                artifactId = 'android-rectangle-detector'
                version = version_name
            }
        }
    }

    tasks.withType(Jar).configureEach {
        if (name == 'releaseJavadocJar') {
            dependsOn(dokkaGenerate)
            from(layout.buildDirectory.dir("dokka/javadoc"))
        }
    }
}
