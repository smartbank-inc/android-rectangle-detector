plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
    id 'org.jetbrains.dokka'
}

group = 'com.github.smartbank-inc'

android {
    namespace 'jp.co.smartbank.rectangledetector'
    compileSdk compile_sdk_version

    defaultConfig {
        minSdkVersion min_sdk_version
        targetSdkVersion target_sdk_version
        versionCode version_code
        versionName version_name

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }

    kotlinOptions {
        jvmTarget = '21'
    }
}

dependencies {
    implementation 'org.opencv:opencv:4.12.0'
    testImplementation 'androidx.test:core:1.4.0'
    testImplementation 'androidx.test:runner:1.4.0'
    testImplementation 'androidx.test:rules:1.4.0'
    testImplementation 'androidx.test.ext:junit:1.1.3'
    testImplementation "junit:junit:$junit_version"
    testImplementation 'org.robolectric:robolectric:4.6.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

afterEvaluate {
    task androidSourcesJar(type: Jar) {
        archiveClassifier.convention('sources')
        archiveClassifier.set('sources')
        from android.sourceSets.main.java.source
    }

    task androidJavadocsJar(type: Jar, dependsOn: dokkaJavadoc) {
        archiveClassifier.convention('javadoc')
        archiveClassifier.set('javadoc')
        from dokkaJavadoc.outputDirectory
    }

    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = 'com.github.smartbank-inc'
                artifactId = 'android-rectangle-detector'
                version = version_name
                artifact androidSourcesJar
                artifact androidJavadocsJar
            }
        }
    }
}
